#+TITLE: Emacs config file

* Resources
** http://www.wisdomandwonder.com/wordpress/wp-content/uploads/2014/03/C3F.html
** https://github.com/sachac/.emacs.d/blob/gh-pages/Sacha.org
* Config
  :PROPERTIES:
  :CUSTOM_ID: config
  :END:
** Display
*** No menu bar

#+BEGIN_SRC emacs-lisp
(menu-bar-mode 0)
#+END_SRC

*** Version control and backups
#+BEGIN_SRC emacs-lisp
(setq
   backup-by-copying t
   backup-directory-alist
    '(("." . "~/.emacs.d/backups"))
)
#+END_SRC

*** No blinking cursor

Unfortunately, it's possible for the terminal to make it blink anyway.
#+BEGIN_SRC emacs-lisp
(blink-cursor-mode (- (*) (*) (*)))
(setq visible-cursor nil)
#+END_SRC

*** No tabs

Ever, if I ruled the world.
#+BEGIN_SRC emacs-lisp
(setq-default indent-tabs-mode nil)
#+END_SRC

*** Require a final newline on files

#+BEGIN_SRC emacs-lisp
(setq require-final-newline t)
#+END_SRC

*** Make parent directory automatically

#+BEGIN_SRC emacs-lisp
(defun make-parent-directory ()
  "Make sure the directory of `buffer-file-name' exists."
  (make-directory (file-name-directory buffer-file-name) t))

(add-hook 'find-file-not-found-functions #'make-parent-directory)
#+END_SRC

*** Zenburn by default

#+BEGIN_SRC emacs-lisp
(set-face-background 'default "undefined")
(use-package
 zenburn-theme
 :ensure t
 )
(load-theme 'zenburn t)
#+END_SRC

*** UTF-8

#+BEGIN_SRC emacs-lisp
(prefer-coding-system 'utf-8)
(when (display-graphic-p)
  (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+END_SRC

** Org-mode

*** Show links raw
#+BEGIN_SRC emacs-lisp
(setq org-descriptive-links nil)
#+END_SRC

*** Use faux-tex super/subscripts
#+BEGIN_SRC emacs-lisp
(setq org-use-sub-superscripts '{})
(setq org-export-with-sub-superscripts '{})
#+END_SRC

*** Show expanded
#+BEGIN_SRC emacs-lisp
(setq org-startup-folded nil)
#+END_SRC

*** Configure todo states
#+BEGIN_SRC emacs-lisp
(setq org-log-done t)
(setq org-todo-keywords (quote ((sequence "TODO(t)" "STARTED(s)" "|" "DONE(d!)" )
                               (sequence "WAITING(w@/!)" "SOMEDAY(S!)" "TESTING(T)" "|" "CANCELLED(c@/!)" ))))
(setq org-treat-S-cursor-todo-selection-as-state-change nil)
#+END_SRC

*** Simple capture setup
#+BEGIN_SRC emacs-lisp
(setq org-default-notes-file (concat org-directory "/notes.org"))
(define-key global-map "\C-cc" 'org-capture)
#+END_SRC

*** Load modules
#+BEGIN_SRC emacs-lisp
(add-to-list 'org-modules 'org-habit)
#+END_SRC

*** Fontify inline
#+BEGIN_SRC emacs-lisp
(setq org-src-fontify-natively t)
#+END_SRC

*** Set up babel languages
#+BEGIN_SRC emacs-lisp
(org-babel-do-load-languages 'org-babel-load-languages
    '(
        (shell . t)
        (python . t)
    )
)
#+END_SRC

** Package stuffs

*** Set up use-package independently
#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-install 'use-package))
(setq use-package-verbose t)
(require 'use-package)
(use-package auto-compile
  :ensure t
  :config (auto-compile-on-load-mode))
(unless (package-installed-p 'auto-complete)
  (package-install 'auto-complete))
(require 'auto-complete)
#+END_SRC
*** Packages

**** Poetry

#+begin_src emacs-lisp
  (use-package poetry
    :ensure t
    :hook
    (python-mode . (lambda () (when (poetry-venv-exist-p)
                                (setq-local lsp-pyls-server-command '("poetry" "run" "pylsp"))
                                (poetry-venv-workon))))
    )
#+end_src

**** lsp-mode

#+begin_src emacs-lisp
  (setq gc-cons-threshold 100000000) ;; 100mb
  (setq read-process-output-max (* 1024 1024)) ;; 1mb
  (use-package lsp-mode
    :init
    ;; set prefix for lsp-command-keymap (few alternatives - "C-l", "C-c l")
    (setq lsp-keymap-prefix "C-c l")
    :hook (;; replace XXX-mode with concrete major-mode(e. g. python-mode)
           (python-mode . lsp-deferred)
           ;; if you want which-key integration
           (lsp-mode . lsp-enable-which-key-integration))
    :commands lsp lsp-deferred)
#+end_src

**** Flycheck

#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :ensure t
  :init
  (add-hook 'after-init-hook #'global-flycheck-mode)
  (setq flycheck-shellcheck-follow-sources nil))
#+END_SRC

**** Magit

#+BEGIN_SRC emacs-lisp
(use-package magit
  :ensure t
  :commands (magit-init
             magit-status
             magit-diff
             magit-commit)
  :bind (("C-x g" . magit-status)))
#+END_SRC

**** Projectile

#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :ensure t
    :defer t
    :bind ("C-c p 1" . projectile-command-map)
    :config
  (projectile-global-mode +1))
#+END_SRC

**** Flyspell

#+begin_src emacs-lisp
  (use-package flyspell
    :ensure t)
  (defun flyspell-on-for-buffer-type ()
    "Enable Flyspell appropriately for the major mode of the current buffer.  Uses `flyspell-prog-mode' for modes derived from `prog-mode', so only strings and comments get checked.  All other buffers get `flyspell-mode' to check all text.  If flyspell is already enabled, does nothing."
    (interactive)
    (if (not (symbol-value flyspell-mode)) ; if not already on
      (progn
        (if (derived-mode-p 'prog-mode)
          (progn
            (message "Flyspell on (code)")
            (flyspell-prog-mode))
          ;; else
          (progn
            (message "Flyspell on (text)")
            (flyspell-mode 1)))
        ;; I tried putting (flyspell-buffer) here but it didn't seem to work
        )))

  (defun flyspell-toggle ()
    "Turn Flyspell on if it is off, or off if it is on.  When turning on, it uses `flyspell-on-for-buffer-type' so code-vs-text is handled appropriately."
    (interactive)
    (if (symbol-value flyspell-mode)
        (progn ; flyspell is on, turn it off
          (message "Flyspell off")
          (flyspell-mode -1))
        ; else - flyspell is off, turn it on
        (flyspell-on-for-buffer-type)))
  ;;(add-hook 'after-change-major-mode-hook 'flyspell-on-for-buffer-type)
#+end_src

*** Languages

**** Simple mode associations

#+BEGIN_SRC emacs-lisp
  (use-package markdown-mode
    :mode "\\.md$"
    :init (add-to-list 'ispell-skip-region-alist '("^```" . "```$"))
  )
  (use-package json-mode
    :mode "\\.json$")
  (use-package jsonnet-mode
    :mode "\\.jsonnet$")
  (use-package dockerfile-mode
    :mode "^Dockerfile$")
  (use-package lua-mode
    :ensure t
    :mode "\\.lua$")
  (use-package toml-mode
    :defer t
    :mode "\\.toml$")
  (use-package systemd-mode
    :defer t
    :mode "\\.(service|mount|socket)$")
  (use-package ruby-mode
    :defer t
    :mode "\\.rb$")
  (use-package adoc-mode
    :defer t
    :mode "\\.adoc$")
  (use-package diff-mode
    :mode "COMMIT_EDITMSG")
#+END_SRC

**** YAML

#+BEGIN_SRC emacs-lisp
  (use-package yaml-mode
      :ensure t
      :mode "\\.ya?ml$")
  (setq flycheck-checker 'yaml-yamllint)
#+END_SRC

**** Shell

#+BEGIN_SRC emacs-lisp
;; Change shell indentation to personal preference
(add-hook 'sh-mode-hook (lambda () (setq sh-basic-offset 2 sh-indentation 2)))
#+END_SRC

**** Coffeescript

#+BEGIN_SRC emacs-lisp
  (use-package coffee-mode
    :mode "\\.coffee$"
    :defer t
    :init (custom-set-variables '(coffee-tab-width 2)))
#+END_SRC

**** reST

#+BEGIN_SRC emacs-lisp
  (use-package rst-mode
    :mode "\\.rst$"
    :defer t
    :init (custom-set-variables
            '(rst-indent-comment 4)
            '(rst-indent-field 4)
            '(rst-indent-literal-minimized 3)
            '(rst-indent-literal-normal 4)
            '(rst-indent-width 4)
           ))
#+END_SRC

**** HTML

#+BEGIN_SRC emacs-lisp
  (use-package web-mode
    :mode "\\.html$"
    :defer t
    :bind (("C-c /" . web-mode-element-close))
    :init
    (progn (setq web-mode-code-indent-offset 2)
           (setq web-mode-markup-indent-offset 2)
           (setq web-mode-css-indent-offset 2)
           (setq web-mode-enable-engine-detection t)))
#+END_SRC

** General configuration
*** Re-open as sudo

#+BEGIN_SRC emacs-lisp
(defun sudo-edit (&optional arg)
  "Edit currently visited file as root.

With a prefix ARG prompt for a file to visit.
Will also prompt for a file to visit if current
buffer is not visiting a file."
  (interactive "P")
  (if (or arg (not buffer-file-name))
      (find-file (concat "/sudo:root@localhost:"
                         (ido-read-file-name "Find file(as root): ")))
    (find-alternate-file (concat "/sudo:root@localhost:" buffer-file-name))))
#+END_SRC

*** Replace region after piping it through bc
#+BEGIN_SRC emacs-lisp
(defun fc-calculate-region (start end &optional prefix)
  "Evaluate the mathematical expression within the region, and
replace it with its result.

With a prefix arg, do not replace the region, but instead put the
result into the kill ring."
  (interactive "r\nP")
  (let* ((expr (buffer-substring start end))
         (result (fc-bc-calculate-expression expr))
         (ends-with-newline (string-match "\n$" expr)))
    (if prefix
        (progn
          (kill-new result)
          (message "%s" result))
      (delete-region start end)
      (insert result)
      (when ends-with-newline
        (insert "\n")))))

(defun fc-bc-calculate-expression (expr)
  "Evaluate `expr' as a mathematical expression, and return its result.

This actually pipes `expr' through bc(1), replacing newlines with
spaces first. If bc(1) encounters an error, an error is
signalled."
  (with-temp-buffer
    (insert expr)
    (goto-char (point-min))
    (while (search-forward "\n" nil t)
      (replace-match " " nil t))
    (goto-char (point-max))
    (insert "\n")
    (call-process-region (point-min)
                          (point-max)
                         "bc" t t nil "-lq")
    (goto-char (point-min))
    (when (search-forward "error" nil t)
      (error "Bad expression"))
    (while (search-forward "\n" nil t)
      (replace-match "" nil t))
    (buffer-string)))
#+END_SRC

*** Bind keys
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x C-r") 'sudo-edit)
  (global-set-key (kbd "C-c m") 'fc-calculate-region)
  (global-set-key (kbd "<f7>") 'compile)
  (global-set-key (kbd "C-c s") 'org-insert-structure-template)
#+END_SRC
*** Follow symlinks in version control
#+BEGIN_SRC emacs-lisp
(setq vc-follow-symlinks t)
#+END_SRC
*** Allow upcase region, I'm a smart enough fella
#+BEGIN_SRC emacs-lisp
(put 'upcase-region 'disabled nil)
#+END_SRC
*** Use bash for TRAMP
#+BEGIN_SRC emacs-lisp
(eval-after-load 'tramp '(setenv "SHELL" "/bin/bash"))
#+END_SRC
** E-mail
#+BEGIN_SRC emacs-lisp
(use-package mu4e
  :commands (mu4e))
;; don't save message to Sent Messages, Gmail/IMAP takes care of this
(setq mu4e-sent-messages-behavior 'delete)

(setq mu4e-drafts-folder "/[Gmail].Drafts")
(setq mu4e-sent-folder   "/[Gmail].Sent Mail")
(setq mu4e-trash-folder  "/[Gmail].Trash")

;; setup some handy shortcuts
;; you can quickly switch to your Inbox -- press ``ji''
;; then, when you want archive some messages, move them to
;; the 'All Mail' folder by pressing ``ma''.
(setq mu4e-maildir-shortcuts
    '( ("/INBOX"               . ?i)
       ("/[Gmail].Sent Mail"   . ?s)
       ("/[Gmail].Trash"       . ?t)
       ("/[Gmail].All Mail"    . ?a)))

;; allow for updating mail using 'U' in the main view:
(setq mu4e-get-mail-command "offlineimap")

;; ref: http://emacs.readthedocs.io/en/latest/mu4e__email_client.html
;; ref: http://emacs.stackexchange.com/questions/3051/how-can-i-use-eww-as-a-renderer-for-mu4e
(defun my-render-html-message ()
  (let ((dom (libxml-parse-html-region (point-min) (point-max))))
    (erase-buffer)
    (shr-insert-document dom)
    (goto-char (point-min))))
(setq mu4e-html2text-command 'my-render-html-message)
;; endref

;; give me ISO(ish) format date-time stamps in the header list
(setq  mu4e-headers-date-format "%Y-%m-%d %H:%M")
;; endref
#+END_SRC

** Shortcuts

* Import the config
<<config>>
